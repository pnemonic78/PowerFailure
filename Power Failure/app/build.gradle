apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'

def versionMajor = Integer.parseInt(project.APP_VERSION_MAJOR)
def versionMinor = Integer.parseInt(project.APP_VERSION_MINOR)

android {
    compileSdkVersion androidBuildSdkVersion

    defaultConfig {
        applicationId "net.sf.power.monitor"
        minSdkVersion androidBuildMinSdkVersion
        targetSdkVersion androidBuildTargetSdkVersion
        versionCode versionMajor * 100 + versionMinor
        versionName versionMajor + '.' + versionMinor.toString().padLeft(2, '0')
        resConfigs "en"

        testApplicationId "net.sf.power.monitor.test"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            storeFile file("../release.keystore")
            storePassword project.STORE_PASSWORD_RELEASE
            keyAlias "release"
            keyPassword project.KEY_PASSWORD_RELEASE
        }
    }

    buildTypes {
        debug {
            debuggable true
            applicationIdSuffix ".debug"
            firebaseCrashlytics {
                mappingFileUploadEnabled false
            }
        }

        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    sourceSets {
        androidTest {
            java.srcDirs += 'src/androidTest/kotlin'
        }
        main {
            java.srcDirs += 'src/main/kotlin'
        }
        test {
            java.srcDirs += 'src/test/kotlin'
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(':android-lib:lib')

    // Testing
    testImplementation 'junit:junit:4.13'
    androidTestImplementation "androidx.test:core:$androidTestVersion"
    androidTestImplementation "androidx.test:rules:$androidTestVersion"
    androidTestImplementation "androidx.test:runner:$androidTestVersion"
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'

    // Logging
    implementation 'com.google.firebase:firebase-crashlytics:17.3.0'
}
