apply plugin: 'com.android.application'

def versionMajor = Integer.parseInt(project.APP_VERSION_MAJOR)
def versionMinor = Integer.parseInt(project.APP_VERSION_MINOR)

android {
    compileSdkVersion Integer.parseInt(project.ANDROID_BUILD_SDK_VERSION)
    buildToolsVersion project.ANDROID_BUILD_TOOLS_VERSION

    defaultConfig {
        applicationId "net.sf.power.monitor"
        minSdkVersion Integer.parseInt(project.ANDROID_BUILD_MIN_SDK_VERSION)
        targetSdkVersion Integer.parseInt(project.ANDROID_BUILD_TARGET_SDK_VERSION)
        versionCode versionMajor * 100 + versionMinor
        versionName versionMajor + '.' + versionMinor.toString().padLeft(2, '0')

        testApplicationId "net.sf.power.monitor.test"
        testInstrumentationRunner "android.test.InstrumentationTestRunner"
    }

    signingConfigs {
        release {
            storeFile file("release.keystore")
            storePassword project.STORE_PASSWORD_RELEASE
            keyAlias "release"
            keyPassword project.KEY_PASSWORD_RELEASE
        }
    }

    buildTypes {
        debug {
            debuggable true
            applicationIdSuffix ".debug"
        }

        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile project(':lib')

    testCompile 'junit:junit:4.12'
}
